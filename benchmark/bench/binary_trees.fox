from fox.io import println;
from fox.profiler import clock;

class Tree {
  __init__(item, depth) {
    this.item = item;
    this.depth = depth;
    if (depth > 0) {
      var item2 = item + item;
      depth = depth - 1;
      this.left = Tree(item2 - 1, depth);
      this.right = Tree(item2, depth);
    } else {
      this.left = nil;
      this.right = nil;
    }
  }

  check() {
    if (this.left == nil) {
      return this.item;
    }

    return this.item + this.left.check() - this.right.check();
  }
}

var minDepth = 4;
var maxDepth = 14;
var stretchDepth = maxDepth + 1;

var start = clock();

println("stretch tree of depth: {}", stretchDepth);
println("check: {}", Tree(0, stretchDepth).check());
println("");

var longLivedTree = Tree(0, maxDepth);

# iterations = 2 ** maxDepth
var iterations = 1;
var d = 0;
while (d < maxDepth) {
  iterations = iterations * 2;
  d = d + 1;
}

var depth = minDepth;
while (depth < stretchDepth) {
  var check = 0;
  var i = 1;
  while (i <= iterations) {
    check = check + Tree(i, depth).check() + Tree(-i, depth).check();
    i = i + 1;
  }

  println("num trees: {}", iterations * 2);
  println("depth: {}", depth);
  println("check: {}", check);
  println("");

  iterations = iterations / 4;
  depth = depth + 2;
}

println("long lived tree of depth: {}", maxDepth);
println("check: {}", longLivedTree.check());
println("elapsed: {}", clock() - start);
